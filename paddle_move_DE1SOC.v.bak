module paddle_move_DE1SOC #(
    parameter SCREEN_WIDTH  = 640,
    parameter SCREEN_HEIGHT = 480,
    parameter PADDLE_WIDTH  = 64,
    parameter PADDLE_HEIGHT = 8,
    parameter PADDLE_Y = 440,
    parameter SPEED = 4
)(
    input  wire clk,
    input  wire rst,
    input  wire [3:0] KEY,
    output reg  [15:0] paddle_x,
    output wire [15:0] paddle_y
);
   
    assign paddle_y = PADDLE_Y;


    // Center paddle at start
    localparam [15:0] START_X = (SCREEN_WIDTH - PADDLE_WIDTH) / 2;


    // Slow down movement using a divider
    reg [19:0] div;
    wire tick = (div == 20'd0);


    always @(posedge clk or posedge rst) begin
        if (rst) begin
            div      <= 20'd0;
            paddle_x <= START_X;
        end else begin
            div <= div + 1'b1;


            if (tick) begin
                // Key movement code
                if (!KEY[0] && KEY[1]) begin
                    // Move left (KEY[0] pressed)
                    if (paddle_x > SPEED)
                        paddle_x <= paddle_x - SPEED;
                    else
                        paddle_x <= 16'd0;
                end else if (!KEY[1] && KEY[0]) begin
                    // Move right (KEY[1] pressed)
                    if (paddle_x + PADDLE_WIDTH + SPEED <= SCREEN_WIDTH)
                        paddle_x <= paddle_x + SPEED;
                    else
                        paddle_x <= SCREEN_WIDTH - PADDLE_WIDTH;
                end
                // If both pressed or none pressed, hold position
            end
        end
    end


endmodule
