module vga_controller(
        input clk,     
        input wire rst,
        output wire H_SYNC,   // horizontal sync
        output wire V_SYNC,  // vertical sync
        

        output wire video_on,
        output wire [9:0] x, // current x position
        output wire [9:0] y // current y position


    );

    parameter H_DISPLAY = 640; // pixels per line
    parameter H_BEFORE_DELAY = 16; // delay period before the sync puls
    parameter H_SYNC_PULS = 96; // how long the horizontal puls lasts before it goes to the next line
    parameter H_AFTER_DELAY = 48;  // delay period after the sync puls 
    parameter H_TOTAL_PIXELS = 800; // The amount of pixels that are covered horizontally 
    

    parameter V_DISPLAY = 480; // pixels per line
    parameter V_BEFORE_DELAY = 10; // delay period before the sync puls
    parameter V_SYNC_PULS = 2; // how long the verical puls lasts before it goes to the next line
    parameter V_AFTER_DELAY = 33;  // delay period after the sync puls 
    parameter V_TOTAL_PIXELS = 525; // total pixes Vertically 

    reg [9:0] h_count; // counts all the horizonal pixels
    reg [9:0] v_count; // counts all the veritcal pixels
    
    reg H_SYNC_REG; // holds value of horizontal sync
    reg V_SYNC_REG; // holds value of vertical sync
    reg VIDEO_ON_REG; // holds value of videos_on

    always @ (posedge clk or posedge rst) begin
       
        if (rst) begin
       
            h_count <= 0;
            v_count <= 0;
       
        end

        else begin
       
           if (h_count == H_TOTAL_PIXELS - 1) begin // counts horizontal
       
            h_count <= 0;

            if (v_count == V_TOTAL_PIXELS - 1) // when a line is done it moves verically
                v_count <= 0;
       
            else
                v_count <= v_count + 1; // moves it vertially
       
        end

        else begin
            h_count <= h_count + 1; // goes row by row horizontally

        end 
      end
    end


    always @ (*) begin
        // h_count >= H_DISPLAY + H_BEFORE_DELAY checking if count has done everything it has needed then we can send the sync
        // h_count < H_DISPLAY + H_BEFORE_DELAY + H_SYNC_PULS checking if we are still in the h_sycn section
        // if the puls is on then H_SYNC_REG = 1'b0
        // if it is off H_SYNC_REG = 1'b0
        if((h_count >= (H_DISPLAY + H_BEFORE_DELAY)) && (h_count < (H_DISPLAY + H_BEFORE_DELAY + H_SYNC_PULS))) begin
            H_SYNC_REG = 1'b0;
        end
        else begin 
            H_SYNC_REG = 1'b1;
        end
        // same as above except it is done vertically rather than horozantaly 
        if((v_count >= (V_DISPLAY + V_BEFORE_DELAY)) && (v_count < (V_DISPLAY + V_BEFORE_DELAY + V_SYNC_PULS))) begin
            V_SYNC_REG = 1'b0;
        end
        else begin 
            V_SYNC_REG = 1'b1;
        end

        // checks if the pixels are within the bouds that we set
        if((h_count < H_DISPLAY) && (v_count < V_DISPLAY)) begin
            VIDEO_ON_REG = 1'b1;
        end
        else begin
            VIDEO_ON_REG = 1'b0;
            
        end     
    end

    // the current positions of x and y that are being done
    assign x = h_count;
    assign y = v_count;

    // it is giving the value to the outpuuts
    assign H_SYNC = H_SYNC_REG;
    assign V_SYNC = V_SYNC_REG;
    assign video_on = VIDEO_ON_REG;

    
    endmodule